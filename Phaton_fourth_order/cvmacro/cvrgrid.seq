!Purpose:	This macro employs the RAYTRA command for computing a grid of 
!           ray data at surface s
!
! Input	
!		^grid	-number of grid lines in X or Y direction
!       ^f		-field number
!		^s		-surface number
!		^w		-wave number
!       ^z		-zoom position
!       ^chkap  -check aperture flag
!
! Output	The output data is stored in buffer 1. Any data outside aperture are 
!		set to be -9999
!
!Authors:	Blair L. Unger, Joseph M. Howard - NASA Goddard		Date: 2-24-2006
!
!*********************************************************************

RFD 32 1 0 1 1 1

lcl num ^grid ^f ^s ^w ^z ^chkap ^i ^j ^k ^rayinput(4) ^rayoutput(8) 
lcl num ^INF ^x0 ^y0 ^L0 ^M0 ^xywidth ^LMwidth ^xystep ^LMstep ^Awidth ^Astep
lcl num ^temp ^Lval ^Mval ^Nval ^dopl ^outstat
^pi == (3.14159265358979)

^grid== #1
^f== #2
^s== #3
^w== #4
^z== #5
^chkap==#6

if ^s=0
    ^s == (num s) ! default to image surface
end if

if (out)
    out n; !turn off all outputs
    ^outstat == 1
else
    ^outstat == 0
end if

if ^s<(num s) !case where surface other than image is desired
    save temp.len
    ^i == ^z+1
    ins z^i
    zoo foc z^i s^s
    ^z == ^i
end if

! Find the x y center and extremes on s1 using reference rays
^x0 == (x f^f r1 w^w z^z s1)
^y0 == (y f^f r1 w^w z^z s1)
^L0 == (L f^f r1 w^w z^z s0) !L data is taken AFTER surface listed
^M0 == (M f^f r1 w^w z^z s0)
^xywidth == maxf(  (y f^f r2 w^w z^z s1)-(y f^f r3 w^w z^z s1) , (x f^f r4 w^w z^z s1)-(x f^f r4 w^w z^z s1) ) 
^LMwidth == maxf(  (L f^f r4 w^w z^z s0)-(L f^f r5 w^w z^z s0) , (M f^f r2 w^w z^z s0)-(M f^f r3 w^w z^z s0) ) 
^xystep == ^xywidth/(^grid-1)
^LMstep == ^LMwidth/(^grid-1)
!^Awidth == maxf(  asinf((L f^f r5 w^w z^z s0))-asinf((L f^f r4 w^w z^z s0)) , asinf((M f^f r3 w^w z^z s0))-asinf((M f^f r2 w^w z^z s0)) ) 
!^Astep == ^Awidth/(^grid-1)

if (typ fld)="ANG"
    ^INF == 1
else 
    ^INF == 0
end if

buf del b1

for ^j 1 ^grid
	^rayinput(2) == ^y0 - ^xywidth/2 + (^j-1)*^xystep
    ^Lval == ^L0 - ^LMwidth/2 + (^j-1)*^LMstep
	for ^i 1 ^grid
        ^rayinput(1) == ^x0 - ^xywidth/2 + (^i-1)*^xystep
        ^Mval == ^M0 - ^LMwidth/2 + (^i-1)*^LMstep
        ^Nval == sqrtf( 1 - ^Lval*^Lval - ^Mval*^Mval )
		^rayinput(3) == ^Lval / ^Nval
		^rayinput(4) == ^Mval / ^Nval
            buf put b1 i^i j^j RAYTRA(^z,^w,^chkap,^rayinput,^rayoutput); 
                !RAYTRA function return:  0 for pass, >0 for surface which missed/TIR/diff
                !                                     <0 for surface which blocked 
            ^k == ^i+^grid; buf put b1 i^k j^j ^rayoutput(1) !x
            ^k == ^k+^grid; buf put b1 i^k j^j ^rayoutput(2) !y
            ^k == ^k+^grid; buf put b1 i^k j^j ^rayoutput(3) !z
            ^k == ^k+^grid; buf put b1 i^k j^j ^rayoutput(4) !L
            ^k == ^k+^grid; buf put b1 i^k j^j ^rayoutput(5) !M
            ^k == ^k+^grid; buf put b1 i^k j^j ^rayoutput(6) !N
            if ^INF !object at infinity
                ^dopl == (^rayinput(1)*^rayinput(3)+^rayinput(2)*^rayinput(4))/sqrtf(1+^rayinput(3)**2+^rayinput(4)**2) !removes tilt across planar wavefront
            else
                ^dopl == 0
            end if
            ^k == ^k+^grid; buf put b1 i^k j^j (^rayoutput(7)+^dopl) !opl
            ^k == ^k+^grid; buf put b1 i^k j^j ^rayoutput(8) !trn
	end for
end for
	
if ^s<(num s) !case where surface other than image is desired
    res
end if

if ^outstat
    out y;
end if

! Copyright © 2004-2005 United States Government as represented by the Administrator of the 
! National Aeronautics and Space Administration.  No copyright is claimed in the United States 
! under Title 17, U.S. Code. All Other Rights Reserved.
! 
! Authors: Joseph M. Howard, Blair L. Unger, Mark E. Wilson, NASA
! Revision Date: 2007.08.22     